# 第2章：变量与元素类型

什么是变量 _(variable)_ ？通俗一点讲，变量是一个“盒子”，是一个用于存储元素值的容器；变量可以存储的元素类型非常广，数值、文本、列表、对象等不同元素类型均可存储。我们可以通过一系列语句来创建变量，并通过读取、修改变量的值，从而最终得到想要的结果。

### 变量的声明及使用
我们不妨把计算机的内存空间想象成一个“快递柜”：这个快递柜有N多个格子，每个格子都可以存放不同尺寸的“物品”；如果需要用到某个格子以存放“物品”，需要先给这个格子加个标签命名，然后再根据这个名称找到对应的格子，并取出或者更换相应的“物品”。当然了，这里的“物品” 并不是现实生活中的某个商品，而是指一个元素值，比如 *数值：25*，*文本：“ABCDE”*，*数组：【1.0，2.1，3.2】* 或者其他元素值。

以下面的程序为例：
```zinc
注1：声明变量，并设置初始元素值
令商品数量、单件重量、总重量 = 0

注2：修改变量
单件重量 = 20
商品数量 = 45

注3：读取变量并运算
总重量 = 商品数量 * 单件重量 
```

1) **声明变量**
   所有的变量均需要先**声明**方可使用 —— 声明变量相当于是从快递柜中找到一个空格子，并对这个格子加标签，表明“这个格子对应的名称是XXX” 。

   变量声明语句以 `令` 字开头，后面以顿号列举出需要声明的变量名称，而后再设置其初始元素值。注意：所有变量在声明的同时都需要设置初值，不允许光声明不设置初值！

   例如，在第一条语句 `令商品数量、单件重量、总重量 = 0` 中，我们一共声明了3个变量，它们的名称分别是 `商品数量`，`单件重量` 及 `总重量`；同时，我们将这3个变量所存的元素值设为 *数值：0*。在这个例子中，`商品数量` 即为**变量名** (*variable name*)，`0` 即为**元素值** (*element*)。

> 创建变量名时需要注意：变量名不能以 `0-9` 开头，诸如 `12345`，`25kg` 等虽然是符合要求的标识符，但是它并不能作为变量名来声明！

2) **读取变量**
   在变量声明完成之后，后面的即可随意读取变量的值 —— 读取变量相当于在快递柜中找到一个对应名称的格子，打开它并取出里面的元素值。一般情况下，读取变量是用于后续的计算，参数的传递，或者干脆直接输出结果。

   **读取变量**的方式非常简单：把变量名写出来即可。比如在最后一条 `总重量 = 商品数量 * 单件重量` 中，等号右边的式子即是读取了 `商品数量` 和 `单件重量` 的值（分别是 `45`和`20`），并计算它们的乘积，然后将结果传递到 `总重量` 变量中。

3) **变量赋值**
   变量之所以被称为“变量”，原因自然是它的元素值是可以修改的。我们将修改变量值的操作称为**变量赋值** —— 变量赋值相当于在快递柜中找到一个名为 “商品数量” 的格子，打开它，用新的元素值去替换里面原来的值。

   赋值语句的语法为 `[变量名] 设为 [元素值]` 或者 `[变量名] = [元素值]`；两者完全等价，没有任何区别。比如 `单件重量 = 20` ，即是将 *数值：20* 直接传递给 `单件重量` 变量中，这意味者原先 `单件重量` 的值是 *数值：0*，执行这条语句后，`单件重量` 的值变成了 *数值：20*。

   等号右边的元素值不仅仅是某个数值，也可能是另外一个变量：比如 `单件重量 = 商品数量` 即是将 `商品数量`的元素值传递给 `单件重量` 变量；或者是一个表达式：比如在 `总重量 = 商品数量 * 单件重量` 中，等号右边的 `商品数量 * 单件重量` 即是一个表达式，在运算得到结果 *数值：900* 后，把这个元素值再传递给 `总重量` 。

### 声明语句的语法
如前所述，**声明语句**的作用是创建变量并给予初值。声明语句支持「单行语法」及「多行语法」；两者皆以 `令` 关键字开头，区别在于是否可以同时声明多行变量。

1. 「单行语法」一次性可以声明一组变量，其结构如下：
```
令 ‹变量列表› ‹声明运算符› ‹初始值›
```

`‹变量列表›` 可以是单个变量名，也可以是多个变量名；多个变量之间用顿号 `、`隔开。每个变量名都需要符合标识符的要求，同时不能以数字开头（诸如 `12kg`的标识符是不能做为变量名使用的）。举个例子： 
```zn
令白马、黑马、红马、黄马 = “千里马”
```
这样就同时声明了四个变量，且它们的初值都是*文本：千里马*。

`‹声明运算符›`  表示变量将以什么样的方式设置初始值。目前 zinc 共支持两种声明运算符：`设为` 、`恒为` 。它们的具体使用方法如下：
1) `设为`  最为常用，它表示将初始值赋予某个变量；例如 `令白马设为“千里马”`；我们通常使用 `=` 替代 `设为` 来赋值，因此 `令白马 = “千里马”` 也是符合语法要求的。 

> 从语法的流畅性而言，诸如 `单件重量设为20` 的赋值语法显然更通顺；但是 `设为` 这两个字输入起来实在太麻烦了，可读性也不是很强；于是我们使用 `=` 来表示 `设为`，这样写起来也舒服一些。 

2) `恒为` 与 `设为` 类似，它不仅将初始值赋予某个变量，同时还标明此变量的值不得更改。 例如在执行 `令白马恒为“千里马”`语句后，直到程序结束，`白马` 变量的元素值都是 `“千里马”`，中途执行诸如 `白马 = “万里马”` 即为报错。

`‹初始值›` 即是需要给到变量的元素值。初始元素值可以是一个数值，也可以是一个变量，或者是一个表达式。例如 `令总重量 = 商品数量 * 单件重量 + 12.5` 。这也引出了一个重要的特性：**每个变量在声明的同时都要设置一个初始值！**

2. 「多行语法」主要是为了方便一次性声明多组变量，其语法如下：

```
令：
    ‹变量列表› ‹声明运算符› ‹初始值› ；‹变量列表› ‹声明运算符› ‹初始值›； ...
    ‹变量列表› ‹声明运算符› ‹初始值› ...
    ...
```

多行语法也是以 `令` 字开头，后面跟着一个冒号`：`，而后*另起一行空四格*，写上要声明的变量及其初始值。一般情况下，一行语句声明一组变量；如果需要在一行语句中声明多组变量，在语句之间加个分号`；` 即可。举个例子：

```zn
令：
    白马 = “千里马”；老牛 = “老黄牛”
    黑马 = “宝马”
    红马、绿马、黄马 = “健康马”；公鸡、母鸡 = “走地鸡”

注：下面的语句若不加缩进，即与上面的令字句无关。
小鸡数量 = 20 
```

### 变量的类型
我们平时所见到的物品多种多样，书本、毛巾、牙刷、手机、汽车、筷子、桌子…… 不一而足；虽然物品种类繁多，但是我们还是可以一些共有的特点，将这些物品归类成数个不同分类：比如手机、电脑、充电宝属于电子产品；毛巾、牙刷、脸盆属于家居用品；汽车、轮船、飞机属于交通工具等等。

在 zinc 语言中，我们可以创建无数个不同的变量，并存放对应的元素值。同样地，这些变量也可归类为不同的类型；变量的类型直接决定了可执行的操作和表达式，比如数值之间可以四则运算，文本之间可以相互拼接等等。

目前 zinc 语言支持 6 种基本类型：
1. 数值 *(number)*
   数值类型是编程语言中最常用的类型，它可以表示一个整数或者浮点数² ；数值之间可以比较大小，也可以进行四则运算。如 `12`，`-31.4`，`+0.3100` 等都是数值类型。

2. 文本 *(string)*
   文本类型是用于表示和处理文本数据的数据类型。它通常由一系列字符组成，可以包括字母、数字和符号，因此又称为*字符串类型*。如 `“千里之行， 始于足下”`。

3. 逻辑 *(boolean)*
   表示逻辑判断的结果。只有 `真` 和 `假` 两个值。

4. 列表 *(list)*
   列表即是一个有多个元素值组成的序列，如 `【1，2，3，4，5】`。

5. 字典 *(map)*
   字典类型是一个存储键值对的复合数据结构。它拥有数个不同名称的标识（做为键），每个标识都有其对应的值；就像是一本字典那样，根据某个条目找到对应的解释内容。例如：`【A = 1，B = 2，C = 3】`。

6. 对象 *(object)*
   _对象_ 这个概念来源于现实生活中的物品，比如一部手机：它是什么牌子的，是否触屏，尺寸多少等等，都是其对应的 _属性 (attribute)_；而使用它发短信，打电话，拍视频等等，都是其对应的 _方法 (method)_；_对象_ 即是特定 _属性_ 和 _方法_ 的集合。

> 对象的概念比较复杂，多们会在后面的章节详细解释对象的创建及使用。

同时，还有一个特殊的类型：空 *(none)*，对应的只有一个值，那就是 `空` 本身。 `空` 值的代表意思是：把它放进去，只是为了证明这个变量是“空”的！听着有点绕？我们不妨把变量想像成一个“盒子”，平时往里面放数值、文本之类的实际物品；但是把 `空` 值放进去，相当于往里面放了一包空气 —— 你说这个盒子有东西吧，它还真有，和“压根没把盒子打开” ¹这事相比还是有作为的；但是你说这一包空气有用吧，它也没什么用，什么都做不了！

在声明变量时，zinc 语言并不限制初值的类型；同时，我们也可以将任意类型的元素赋值给变量。唯须注意，每个类型都有其独有的属性、方法及运算方式，如果使用错误的变量类型，则会出现不符合预期的结果，甚至直接抛出异常！

-----
注1：Java 语言的 `null` 就是“压根没把盒子打开”的典范：如 `String a = null;` ，变量 `a`  虽然属于 `String` 类型，但因为这个变量里面什么都没有，类型检查自然也能通过；事实上，zinc 语言对*空*类型以及`空`值，更接近于 Scala 等一众函数式编程语言当中的 `None` ，它属于 `Option` 类型下的子类型。

注2：浮点数是一种数值在计算机内存中的二进制表示方式，它由符号位、阶码和尾数三部分组成；这里可以将浮点数近似地理解为诸如 `3.14`，`0.125`的小数。