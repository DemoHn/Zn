# 第6章：文本的表示及格式化

在日常编程过程中，我们不仅需要处理数值，还常常需要处理文本信息。在 zinc 语言中，文本类型是一种重要的数据类型，它用于表示一系列的字符，就像我们在日常生活中使用的文字一样。

### 文本与字符串

文本类型通常又被称为*字符串*，而字符串又是由一系列 *字符* 串起来的 —— 就像一串糖葫芦一样把一个个”字符“ 按照先后顺序 “串” 起来。例如 `Hello` 这个字符串，它里面有 5 个字符，从前往后数第一个是`H`，第二个是`e`，第三个是`l`，第四个是`l`，第五个是`o`。

字符是构成字符串的基本单位，它可以是一个字母（比如`A`），一个数字（比如`1`），或者一个汉字（比如`千`）等等。每一个字符都有其对应的唯一编码以便管理，就像每个人都有一个独一无二的身份证号码来区分彼此一样。在 zinc 语言中，我们采用国际通行的 Unicode 编码¹ 来管理字符；

字符的 Unicode 编码通常以 `U+` 开头，后面跟上其编码的16进制表示（如字符 `A` 对应的 Unicode 编码为 `U+0041`，字符 `千` 对应的 Unicode 编码为 `U+5343`）在某此情况下，我们可以直接使用 Unicode 编码来指定字符串里的某个字符。

### 文本的表示

我们可以直接在程序中输入一串字符来表示一个文本。为了将文本元素与关键词作区分，我们需要在其左右两边使用双引号`“ ”` 或者直角引号`「 」`括住以创建文本，例如：

```zinc
令示例文本 = “千里之行，始于足下”
令另一文本 = 「Hello World」
```

第一行等号（`=`）右边的值即是一个文本类型的元素值，它由 9 个字符组成；同样的，第二行右边也是一个文本 `Hello World`，它由11个字符组成。（中间的空格也是一个字符 `U+0020`!）

**多行文本**的创建相当简单：直接敲个回车即可，如下例所示：

```zinc
令静夜思-李白 = “
床前明月光
疑是地上霜
举头望明月
低头思故乡”
```

值得注意的是，在文本当中也可以直接将双引号成对加进来，而不需要做特殊的处理，如下例所示：

```zinc
令对话 = “小狗说：“旺旺”！”
```

其中有两对 `“ ”` 双引号，外面的一对即代表了文本的范围，里面的引号即是文本的一部分。因此，`对话` 所对应文本的其实是 `小狗说：“旺旺”！` ，共计9个字符。

> ⚠️ 在文本中添加引号时要注意成双成对，否则会出现解析异常的情况。详见「引号的搭配」一节。

##### 特殊字符的表示

如前所述，zinc 语言的字符统一采用 Unicode 编码；在某些情况下，我们需要在文本中添加一些不方便直接输入的特殊字符，这时可以采用 `` `{特殊字符的英文缩写}` `` 的格式（注意左右两边各有一个 `` ` `` 反引号）来表示。

下表列举了目前 zinc 语言所支持的所有特殊字符的表示形式：


| 字符名  | Unicode 编码     | 表示形式     | 备注                                      |
| --------- | ------------------ | -------------- | ------------------------------------------- |
| `\r`    | `U+000D`         | `` `CR` ``   | 换行符的一部分                            |
| `\n`    | `U+000A`         | `` `LF` ``   | 换行符，Linux Mac 等系统默认使用此换行符  |
| `\r\n`  | `U+000D  U+000A` | `` `CRLF` `` | 这里表示的是两个字符！Windows系统默认使用 |
| `\t`    | `U+0009`         | `` `TAB` ``  | 制表符                                    |
| `` ` `` | `U+0060`         | `` `BK` ``   | 反引号自身                                |
| ` `     | `U+0020`         | `` `SP` ``   | 空格                                      |

同时，我们可以使用 `` `U+XXXX` `` 的格式来表示任意 Unicode 字符（其中 `XXXX` 代表其 Unicode 编码值，以 1~8位 16进制的数字 `[0-9A-F]`表示），如 `` `U+1F005` `` 即代表了 🀅 这个字符。

##### 引号的搭配

每一个文本都会在其外围用双引号括住以标识。在某些情况下，我们需要在文本中直接添加双引号 —— 在处理这种文本时一定要非常谨慎，否则就会出现歧义，如 `“甲乙”丙丁”`，这究竟是想表达字符串 `甲乙` 还是字符串 `甲乙”丙丁` 呢？

为了避免歧义，对文本内添加引号要求如下：

1. 如果引号可以配对（即前面有左引号，后面有右引号），那就直接添加即可。如 `“甲“乙”丙丁”` 或者 `“一“二“三””四五”`
2. 如果引号不能配对（比如单独一个左引号或右引号），那就需要在引号的两边各添加一个 `` ` ``  （反引号），如 ``“甲乙`”`丙丁”`` 或者 ``“甲`”`乙`“`丙丁”``

### 文本的格式化

zinc 语言的文本格式化功能支持更丰富的用法，允许你在模板中灵活插入和格式化变量，尤其是对数值的格式化有较强的表达能力。其基本语法为：

```
“文本模版” % 参数
```

* “文本模板”：包含一个或多个占位符（如 `{}`、`{#}` 等）的字符串。
* 参数：是一个数组（用【】括起来），用于依次填充模板中的占位符。

##### 基本用法

- 使用 `{}` 作为占位符，变量会按顺序插入到对应位置。
- 例：

  ```zinc
  “A{}” % 【“BCD”】           // 结果为 “ABCD”
  “{}年{}月{}日” % 【2025，7，3】    // 结果为 “2025年7月3日”
  ```

#### 数值格式化

在 `{}` 内可以使用 `#` 开头的格式化指令，对数值进行定制化显示：

- `{#}`：以默认方式格式化数字（6位有效数字，类似 Python 的 `%.6g` 格式）

  ```zinc
  “数值为{#}” % 【123.456789】   # 结果为 “数值为123.457”
  ```
- `{#.N}`：保留 N 位小数

  ```zinc
  “保留两位小数：{#.2}” % 【3.14159】   # 结果为 “保留两位小数：3.14”
  ```
- `{#+}`：正数和零前加 `+` 号

  ```zinc
  “变化：{#+}” % 【5】   # 结果为 “变化：+5”
  “变化：{#+}” % 【-3】  # 结果为 “变化：-3”
  ```
- `{#.N%}`：以百分比显示，保留 `N` 位小数

  ```zinc
  “完成率：{#.1%}” % 【0.876】   # 结果为 “完成率：87.6%”
  ```
- `{#.NE}`：科学计数法显示，`N` 为小数位数

  ```zinc
  “科学计数：{#.2E}” % 【12345】   # 结果为 “科学计数：1.23E+04”
  ```

##### 多参数格式化

模板中可以有多个格式化占位符，参数按顺序依次填充：

```zinc
“姓名：{}，分数：{#.1}” % 【“张三”，98.765】   # 结果为 “姓名：张三，分数：98.8”
```

##### 注意事项

- 如果模板中的占位符数量与参数数量不一致，会报错。
- 如果格式化类型与参数类型不匹配（如 `{#}` 用于字符串），也会报错。


注1：每个字符对应的 Unicode 编码可从 https://symbl.cc/en/unicode-table/ 中查阅
