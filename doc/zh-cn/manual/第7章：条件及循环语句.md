# 第7章：条件及循环语句

在程序设计中，条件判断和循环控制是实现复杂逻辑的基础。zinc 语言提供了直观、接近自然语言的条件与循环语句，帮助你灵活地控制程序流程。

### 条件判断语句

所谓「条件判断」，即是根据不同的输入条件，执行对应的代码逻辑。

条件判断语句以「如果」开头，后面跟上用于判断的输入条件，当输入条件为 `真` 时，执行下面所对应的逻辑，否则就不执行/执行其他分支。

> 注意：输入条件须是一个完整表达式，其值为`真` 或 `假`；不接受诸如`空`，`0` 的 falsy value.

**`如果` 句式**

此种情况下只用「如果」语句；当且仅当输入条件为 `真` 时执行代码，其他情况下不执行程序。例如：

```zinc
如果天气为“晴”：
    输出 “可以去郊游！”
```

**`如果-否则` 句式**

在上面的基础之上，我们可以更进一步：当判断条件为 `真` 时，执行一部分逻辑，当判断条件为 `假` 时，执行另外一部分逻辑。例如：

```zinc
如果分数 >= 60：
    输出 “及格”
否则：
    输出 “不及格”
```

**`如果-否则-再如` 句式**

再进一步，当我们需要用到多个分支时，我们可以加入「再如」语句去扩展多个分支的判断逻辑。`再如` 分支可以有多个；最后可以加一个 `否则` 语句兜底。

```zinc
如果分数 >= 90：
    输出“优秀”
再如分数 >= 80：
    输出“良好”
再如分数 >= 60：
    输出“及格”
否则：
    输出“不及格”
```

#### 4. 嵌套条件语句

条件语句可以嵌套使用，实现更复杂的判断逻辑。

```zinc
如果天气为“晴”：
    如果温度 > 30：
        输出“注意防晒”
    否则：
        输出“适合出行”
否则：
    输出“建议在家休息”
```

---

### 循环语句

我们可以使用循环执行语句让程序重复执行某一段逻辑。

#### 1. `每当` 句式

「每当」语句以「每当」关键词开头，后面跟着一个判断条件，当这个判断条件的值为 `真` 时，后面的执行逻辑将会执行一遍又一遍，直到判断条件不为`真`时，整条语句才会停止执行。

例如：

```zinc
令总和 = 0
令N = 1

每当N <= 100：
    总和 = N + 总和
    N = N + 1
输出总和
```

> 注意：使用「每当」语句的判断条件时一定要非常小心；如果判断条件永远是 `真` 的话，此语句会一直执行，永不停歇 —— 亦即所谓「死循环」。以下即是一个「死循环」的典型例子：
>
> ```zn
> 每当真：
>     N = N + 1
> ```

#### 2. `遍历` 句式

用于遍历数组、哈希表等集合。

**遍历数组：**

```zinc
令数组 = 【“苹果”，“香蕉”，“橘子”】
以水果遍历数组：
    输出水果
```

**遍历数组并获取下标：**

```zinc
令数组 = 【“苹果”，“香蕉”，“橘子”】
以 下标、水果 遍历 数组：
    输出 “第{}个水果是{}” % 【下标，水果】
```

**遍历哈希表：**

```zinc
令字典 =【 A = 1, B = 2】
以键、值遍历字典：
    输出 “{} 的值是 {}” %【键，值】
```

#### 3. `结束循环` 与 `继续循环`

- `结束循环`：跳出当前循环
- `继续循环`：跳过本次循环，进入下一轮