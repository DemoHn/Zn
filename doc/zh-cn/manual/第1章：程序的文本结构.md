# 第1章：程序的文本结构

### 文本结构
一个 zinc 程序是由文本编辑而成 —— 既不是一张电路图，也不是一张照片；换句话讲，只要打开记事本，在上面输入一些符合 zinc 语法的文本，保存下来之后即是一个可运行的 zinc 程序文件。¹ 

以下文本即是一个可执行的 zinc 程序，接下来我们将以这个程序为例介绍它的文本结构：
```zinc
输入温度、单位

注1：温度单位有两种，一种是“摄氏度” (°C)，一种是“华氏度” (°F) ；以下逻辑实现了温度互转：输入°C，输出°F值，反之亦然
如果单位为“摄氏度”：
    输出32 + 1.8 * 温度
再如单位为“华氏度”：
    输出{温度 - 32} / 1.8
否则：
    抛出异常：“无效的温度单位”！
```

##### 语句与缩进
一个完整程序是由一条条语句累积而成。语句严格遵循程序语法，并表示明确的执行指令。一条语句通常占用程序的一行，但很多情况下也会占用很多行：比如第4行~第9行都属于一条 `如果` 语句。

有细心的读者会注意到：第4行~第9行属于一条 `如果` 语句，第5行 `输出32 + 1.8 * 温度` 也是一条完整的语句（它的意思是：“计算 「32 + 1.8 * 温度」的结果，将其输出到程序外部，并结束整条程序”）！是的，语句里面还可以嵌套语句；为了正确的表示嵌套关系，我们使用「缩进」来标识不同层级的语句块。²

**关于缩进：**“缩进” _(indent)_ 一词最早来自于印刷行业，指的是在某行或某个段落前留上空白距离。而在 zinc 中，我们使用空白符（空格或`TAB`）作为缩进，并使用固定数量的空白符来表示一个单位缩进：一个单位缩进为 `4个空格 (U+0020)` 或者 `1个Tab (U+0009)` 字符。空格和Tab在同一个程序里不能混用，亦即若前一行以空格作为缩进符，则后一行不能以TAB作为缩进符。

缩进是可以叠加的：例如在一行前面加上 12个空格 对应 _缩进值为3_；或者说用 4个Tab 对应 _缩进值为4_。硬性规定4个空格做为单位缩进主要是考虑到美观：毕竟在中文里空两格要比空一格好看不少。

##### 注释
为了提高程序的可读性，zinc 语言支持在程序文本之上添加注释 _(comment)_。注释的内容不参与到程序的执行当中，仅仅做到解释说明的作用；比如第3行就是一整条注释 —— 这一行的所有内容都不会解析并执行。

zinc 语言使用的注释分为两种：**单行注释**及**块注释**；它们使用方式分别如下所示：

_a) 单行注释_
单行注释以 `注：` 或者 `//` 开头，这一行后面的所有内容都是作为注释的一部分；同时，在 `注` 字后面加上任意个数的 `0~9` 之间的数字都是可以的，比如 `注12：`（数字并没有特别的含义，纯粹只是供阅读者标记方便之用）

举几个例子说明：
```zinc
如果其名为「小明」：  注1：此行的「为」字并非代表赋值，而是作逻辑判断用。
    （显示：「小明的成绩」）
令结果 = A + B // 计算两个数之和
```
   
第一行的 `注1：此行的「为」字并非代表赋值，而是作逻辑判断用。` 和第三行的 `// 计算两个数之和` 即是整个单行注释的内容，它们分别解释了前面的程序起到什么作用。

_b) 块注释_
与单行注释不同，块注释的内容可以是多行文本 —— 这意味着如果注释内容多到一行写不下的话，就可以使用块注释来表示。块注释有两种方式：`注：“  [注释内容]  ”` _（可以将双引号 `“ ”` 替换成 `「 」`，两者是等价的）_ 或者 `/*  [注释内容]  */`  

举几个例子说明：
   ```zinc
   注2：「
       此方法主要用于计算当前商品的总价。
       参数1：商品SKU ID
       参数2：商品数量
       参数3：商品折扣
       输出值：商品总价
   」
   
   /* 这个内容也是
      嗯....
      传统的多行注释 */
   ```

##### 标点符号
标点符号 _(punctuation)_ 的主要用途是辅助构造语句 —— 就像标点符号在一篇文章当中的作用一样。如果一个程序满眼都是汉字而没有一个标点符号，那阅读起来可真是一场灾难！

目前可用的标点符号共22个，包括逗号、顿号、引号、括号、问号、冒号等等……这些标点符号在中文输入法下皆可流畅输入，故而在写程序时无需切换输入法。现将所有标点符号列举如下（具体的用途会在后面章节一一介绍）：

| 名称  | 标点符号  |       | 备注                |
|-----|-------|-------|-------------------|
| 双引号 | **“** | **”** |                   |
| 双引号 | **「** | **」** | `“ ”` 与 `「」` 完全等价 |
| 单引号 | **‘** | **’** |                   |
| 单引号 | **『** | **』** | `‘ ’` 与 `『』` 完全等价 |
| 书名号 | **《** | **》** |                   |
| 方括号 | **【** | **】** |                   |
| 圆括号 | **（** | **）** |                   |
| 花括号 | **{** | **}** |                   |
| 逗号  | **，** |       |                   |
| 顿号  | **、** |       |                   |
| 冒号  | **：** |       |                   |
| 分号  | **；** |       |                   |
| 问号  | **？** |       |                   |
| 叹号  | **！** |       |                   |

##### 运算符
运算符 _(operator)_ 通常用于表达式的运算及比较。目前可用的运算符共16个，它们虽然都是英文字符，但是在中文输入法下**大部分**都可以流畅输入（亦即不用切换英文输入法）

> `<` 和 `>` 这两个符号在大部分键盘下的键位在 `M` 按键的右边两个；但是在中文输入法的环境下，Shift + 这两个键输出的是书名号 `《 》`；因此若要正常输入 `<` 和 `>` ，要么切换回英文输入法输入，要么在输入法中做设置，修改输出的字符。

现将所有的运算符按照功能列举如下（具体的用途在后面的章节一一介绍）：

| 四则运算符 | 数值比较符  | 赋值运算符     | 地址引用符     |
|-------|--------|-----------|-----------|
| **+** | **==** | =         | **&**     |
| **-** | **>**  |           |           |
| *     | **<**  | **类型标注符** | **成员指定符** |
| **/** | **>=** | **@**     | **#**     |
| \|    | **<=** |           |           |
| **%** | /=     |           |           |

##### 关键词
所谓关键词 _(keyword)_，即是指在 zinc 语言中预定义并保留使用的词语；这些词语具有特定的意义，用于表达程序的结构或指定某种操作：比如第1行的 `输入` ，第4行的 `如果` ，第6行的 `再如` 都是关键词。

zinc语言全部采用中文关键词。目前共使用34个关键词，字数分别从1-4个不等；以下是按字数分类的关键词列表：

| 一字词 (8个) | 二字词 (21个) |     |     | 三字词 (3个) | 四字词 (2个) |
|----------|-----------|-----|-----|----------|----------|
| 令        | 设为        | 输出  | 否则  | 不等于      | 继续循环     |
| 为        | 恒为        | 如何  | 每当  | 不大于      | 结束循环     |
| 以        | 新建        | 拦截  | 遍历  | 不小于      |          |
| 其        | 何为        | 导入  | 等于  |          |          |
| 或        | 不为        | 定义  | 大于  |          |          |
| 且        | 如果        | 得到  | 小于  |          |          |
| 之        | 再如        | 输入  | 抛出  |          |          |
| 的        |           |     |     |          |          |

##### 标识符
所谓标识符 _(identifier)_ ，即是指在程序中标识特定事物的词语。标识符由用户自行定义，由1个或数个连续的字符组成。根据使用位置的不同，它可以是 _变量名_，也可以是 _方法名_、_类型名_ 等等。例如 `温度`、`单位` 都属于标识符，在上面的示例程序中代表了 _变量名_。

在 zinc 语言中，可用作标识符的字符范围³ 非常广：除了标点符号及*部分*运算符之外，英文字母、数字、汉字、日文、韩文等都可用作标识符；和其他编程语言不同， **`+` 、 `-`、  `*`、`/`  这四个运算符也可视为标识符的一部分**，但有几条特别的限制：
1) `*`、`/` 不能作为标识符的开头
2) `/` 不能作为标识符的结尾

以下是符合要求的标识符的例子：

| 标识符              | 备注               |
|------------------|------------------|
| ijk              | 纯英文              |
| H20              |                  |
| _WINDOW_HANDLER2 | 下划线可以用           |
| $xyz             | 标识符可以用 `$` 开头    |
| 12.345           | 数值也属于 `标识符` 的一部分 |
| +45.78           | `标识符` 可以用 `+` 开头 |
| 某变量              | 纯中文              |
| フライドポテト          | 片假名              |
| 星标值*             | `标识符` 后面也可以带 `*` |
| 白卡纸/牛皮纸飞机盒       | `标识符` 中间可以带 `/`  |
| 公交车站-数目          | `标识符` 中间可以带 `-`  |
| -内部属性-           | `标识符` 头尾也可以带 `-` |

##### 字符串
**字符串** *(string)* 一词顾名思义，即是将一个个字符像串羊肉一样从前到后串起来⁴；在 zinc 语言中，我们使用字符串来表示文本类型的值。标识一个字符串的方法很简单：只要用引号括住里面的内容即可。比如第 4 行的 `“摄氏度”` 即是一个典型的字符串。

目前，zinc 语言共支持三种符号来标识一个字符串：两种*双引号* `“ ”` 和 `「 」`，以及一种书名号 `《 》` 。字符串的内容可以是任意 Unicode 字符，包括空格、换行符、数字、标点、emoji、甚至是引号本身！

> 唯须注意：字符串内容中的引号需要配对，不能单个存在。比如字符串的前面出现了 `“` 字符，那么在后面就需要有对应的 `”` 字符。需要配对的引号有：
>     a. 英式双引号 `“ ”`
>     b. 中式双引号 `「 」` 
>     c. 英式单引号 `‘ ’` 
>     d. 中式单引号`『 』` 
>     e. 双书名号 `《 》` 
>     f. 单书名号 `〈 〉`

以下例子都是符合要求的字符串：

1) 正常的字符串
   ```zinc
   「千里之行，始于足下。」
   ```    
   _开引号为`「`，文本内容为`千里之行，始于足下。`，闭引号为 `」`。_

2) 嵌套引号的字符串
   ```zinc
   《《论语》〈学而篇〉集注》
   ```
   _即使外层引号为`《》`，内层文本内容亦可包含`《》`，唯须注意文本内容里面的`《》〈〉`必须配对。_

3) 多行字符串
   ```zinc
   “朝辞白帝彩云间，
   千里江陵一日还。
   两岸猿声啼不住，
   轻舟已过万重山。”
   ```
   _对于多行文本的处理和单行文本相同，换行符直接加上去就可以了。_

4) 包含标点符号的字符串
   ```zinc
   “如果`游所为的手机`是“华人牌”：那就可以去找陆小千、猪八戒、孙悟空一起维护正义😊！”
   ```

### 标识符与关键词的切分逻辑
zinc 语言采用*无空格设计*，关键词与标识符之间无需添加空格也可自动切分；为实现此设计，zinc 语言采用 “贪婪匹配” 的原则，亦即在一段代码中，如果某几个字连在一块正好是某个关键词，那么这几个字就被征用成关键词，而剩下的字再拼起来作为标识符，无论前后是否有空格！我们不妨举几个例子：

1) `游所为的手机`
上面这6个字看起来是一个完整的标识符，但因为中间存在关键词 `为` 和关键词 `的`， zinc 语言会自动将这两个字提取成关键词，而剩下的字符则是这段代码将会解析成如下结构： 

```
   游所    +    为    +    的    +    手机 
（标识符）  （关键词） （关键词）  （标识符）
```

2) `价格不大于20`
虽然 `大于` 和 `不大于`  都是关键词，但因为第三个字 `不` 和后面的 `大` 和 `于` 连在一起正好是一个关键词，所以优先组合 `不大于` 。这段代码将会解析成如下结构：

```
   价格    +    不大于   +    20 
（标识符）    （关键词）  （标识符）
```

3）`黑/白款餐垫-数量 / 每箱数量`
只要标识符中间添加了空格，标识符就会分开解析成多个；在上面的例子里，第二个 `/` 左右两边各加了一个空格，因此第二个 `/` 左右两边各是一个完整的标识符。与之相对的，第一个 `/` 左右两边并没有空格，因此 `黑/白款餐垫-数量` 即是连在一起成为一个完整的标识符。这段代码将会解析成如下结构：

```
黑/白款餐垫-数量      +      /      +    每箱数量
   （标识符）        （运算符：除号）   （标识符）
```

> 顺便一提，上面这段代码的意思是 *将 “黑/白款餐垫” 所生产的总数量除以每箱可装的数量，最后得到所需要的箱数*。

4) ``如果`游所为的手机`为“华人牌”：``
在某些情况下，如果在命名标识符确实需要包含某些关键字的话，我们可以在标识符的前后各加一个反引号 **\`** （Unicode编码是U+0060，位于键盘的左上角，按键`1`的左边） ，即代表着：反引号内括住的内容都是标识符的一部分，请不要在里面自动提取关键词。这段代码将会解析成如下结构：

```
   如果    +    游所为的手机    +    为    +    “华人牌”    +    ：
（关键词）       （标识符）      （关键词）   （字符串）（标点符号-冒号）
```

##### 程序文本的解析优先级

对于一段程序文本，它会因为上下文不同而表示不同的意义。比如 `「命令」` 一词，正常应作为字符串处理，而若是在注释区块中即代表注释内容的一部分。下表展现出文本解析优先级：其中，数字越大代表优先级越高。若是高优先级内容（例如注释）包含了低优先级的内容（例如文本），则低优先级的内容将被视作高优先级内容的一部分。

| 优先级 | 语素名称  | 示例              |
|-----|-------|---------------------|
| 5   | 注释    | 注：“这个“\`华为手机\`”是注释” |
| 4   | 字符串   | “这是一个\`华为手机\`”      |
| 3   | 标注标识符 | \`华为手机\`            |
| 2   | 关键字   | 为                   |
| 1   | 标识符   | 手机                  |

---- 
*注1：* zinc 程序文本统一采用 Unicode 编码，故文件须以 `UTF-8` 编码储存。采用其他编码（包括但不限于 `GB2312`, `Big5`, `GBK`等）存储的文件将会被认为是乱码，进而导致程序无法正确执行。由于历史原因，Windows 系统在简体中文环境下的默认编码是 GBK，这导致直接用词本保存的文件默认GBK编码，从而导致程序解析失败；这也导致了人们对“中文编程容易导致乱码”的刻板印象。

*注2：* 关于缩进 (indent) 的概念及应用，可以具体了解 [Python](https://www.python.org/)语言的语法 —— 就缩进而言，zinc 和 Python 采用的是相同的策略。

*注3：*此处的字符指的是 Unicode 字符；同时为了保证字符之间不被混淆，部分相近的冷门 Unicode 字符是不能作为标识符的。

注4：稍微严格且学术一点的定义：字符串是指*由零个或者多个字符组成的有限序列*。
