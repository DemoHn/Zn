## BNF

Zn 语言的语法如下述所示（以`EBNF`格式*）

> 1. 以 `‹ ›` 括住的表示非终结符（如 `‹表达式›`）
> 2. 以 `'` 在前面表示部分终结符，如 `'数值`
> 3. 符号 (symbol）会直接显示，不会用单引号括住，如 `如果`
> 4. `::=` 表示定义，如 `‹表达式› ::= 如果 ‹数值›`
> 5. `*` 表示内文存在于0次或N次 (N > 0)
> 5. `+` 表示内文存在于1次或N次 (N > 1)
> 6. `?` 表示内文存在于0次或1次 （注意和全角`？`区分）
> 7. `^` 放在前面表示后面的语句需要换行，并多一个缩进
> 8. `[ ]` 表示组合（因为 Zn 语言中只会用 `【 】` 作为符号，而不会用 `[ ]`）

```
‹语句块›       ::=  ‹语句›  [‹间隔符›  ‹语句›]*

‹语句›         ::=  ‹令之语句›
                |  ‹如果语句›
                |  ‹循环语句›
                |  ‹如何语句›
                |  ‹输出语句›
                |  ‹遍历语句›
                |  ‹定义语句›                
                |  ‹以之语句›
                |  ‹导入语句›
                |  ‹表达式›
                |  ；

‹令之语句›     ::=  令  ‹赋值对›  ‹赋值对›*
                |  令  ： ^[‹赋值对›  ‹赋值对›*]

  ‹赋值对›     ::=  ‹标识列表›  ‹为字词›  ‹表达式›
                |  ‹标识列表›  恒为  ‹表达式›
                |  ‹标识列表›  成为  'ID  [：  ‹表达式›  [、 ‹表达式›]*]?

  ‹标识列表›   ::=  'ID  [、  'ID]*

‹如果语句›     ::=  如果  ‹表达式›  ：  ^‹语句块›  [‹换行符›  再如  ‹表达式›  ：  ^‹语句块›]*  [‹换行符›  否则  ‹表达式›  ：  ^‹语句块›]?

‹循环语句›     ::=  每当  ‹表达式›  ：  ^‹语句块›

‹如何语句›     ::=  如何  'ID  ？  ^[[已知  'ID  [、  'ID]*]?  ‹换行符›  ‹语句块›]

‹输出语句›     ::=  输出  ‹表达式›

‹遍历语句›     ::=  遍历  ‹表达式›  ：  ^‹语句块›

‹定义语句›     ::=  定义  'ID  ：  ^‹属性列表›  [^‹构造语句›]?  [^‹方法列表›]*

  ‹属性列表›   ::=  ‹属性定义句›  [‹换行符›  ‹属性定义句›]*

  ‹构造语句›   ::=  是为  'ID  [、  'ID]*

  ‹方法列表›   ::=  ‹如何语句›
                |  ‹何为语句›                

  ‹属性定义句› ::=  其  'ID  ‹为字词›  ‹表达式›

  ‹何为语句›   ::=  何为  'ID  ？  ^[[已知  'ID  [、  'ID]*]?  ‹换行符›  ‹语句块›]

‹以之语句›    ::=  以  'ID  [、  'ID]*  ‹遍历语句›
               |  以  'ID  [、  'ID]*  （  'ID  [：  ‹表达式›  [，  ‹表达式›]+]?  ） 

‹导入语句›    ::=  导入  '文本  [‹之字词›  'ID  [、  'ID]*]?

‹表达式›      ::=  ‹且表达式›  [或  ‹且表达式›]*

‹且表达式›    ::=  ‹比较表达式›  [且  ‹比较表达式›]*

‹比较表达式›   ::=  ‹赋值表达式›  [[等于  |  =  |  不等于  |  /=  |  小于  |  <  |  不小于  |  >=  |  大于  |  >  |  不大于  |  <=]  ‹比较表达式›]*

‹赋值表达式›   ::=  ‹成员表达式›  [‹为字词›  ‹成员表达式›]?
               |  ‹成员表达式›  [‹为字词›  &  ‹成员表达式›]?

‹成员表达式›   ::=  其  'ID  ‹为字词›  ‹索引表达式›
               |  ‹基础表达式›  ‹索引表达式›

‹索引表达式›   ::=  #  '文本  ‹索引表达式›                
                |  #  {  ‹表达式›  }  ‹索引表达式›
                |  ‹之字词›  ‹属性子式›  ‹索引表达式›

‹属性子式›     ::=  'ID
                |  （  'ID  [：  ‹表达式›  [，  ‹表达式›]+]?  ） 
                |  以  'ID  [、  'ID]*  （  'ID  [：  ‹表达式›  [、  ‹表达式›]+]?  ） 

‹基础表达式›   ::=  {  ‹表达式›  }
                |  ‹属性子式›
                |  对于  ‹表达式›  （  'ID  [：  ‹表达式›  [、 ‹表达式›]*]?  ）  [得到  'ID]?
                |  '数值
                |  '文本
                |  ‹数组表达式›

‹数组表达式›   ::=  【  =  】
                |  【  ‹表达式›  [、  ‹表达式›]+  】
                |  【  ‹键值对›  [‹键值对›]+  】

‹键值对›      ::=  'ID  =  ‹表达式›
               |  '文本  =  ‹表达式›
               |  '数值  =  ‹表达式›

‹为字词›      ::=  为  
               |  是

‹之字词›      ::=  之
               |  的

‹间隔符›      ::= [ ；  ‹换行符›]+

‹换行符›      ::=  [\r  |  \n  | \r\n  |  \n\r]+
```