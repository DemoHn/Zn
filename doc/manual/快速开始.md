# 快速入门

## 写在前面

本篇教程旨在向没有编程基础，或者刚刚入门编程的新手，以实际案例快速介绍 Zn 的常用语法及使用 Zn 进行编程的方法。如果您是一位有着丰富开发经验的老手，您可以选择直接阅读我们的《用户手册》以了解 Zn 的细节。

所谓「实践出真知」，这里在阅读本篇教程时建议同时打开编辑器，将里面的例程自行再输入一遍，并运行程序，观察结果。作为一门中文编程语言，请大家务必在输入程序时注意区分「全角」和「半角」标点符号：一般情况下，只要切换到中文输入法，即可输入程序中所需要的标点符号而无需切换。

祝阅读愉快！

## 变量的声明

在计算机编程领域，变量用于标识某个存储的值或者内容；这里的「变量」类似于数学函数里面的「变量」，亦即「可变的量」。变量的值可以随时读取，亦可以通用「赋值」操作修改其对应的值。

在 Zn 中，所有变量必须 **先声明后使用**：

```zn
令生产总值 = 120
生产总值 = 200
令日期 为「2021-09-25」
```

如上面的代码所示，第1行的 `令生产总值 = 120` 即是声明变量： 其中，`令` 是 Zn 中的一个关键字，用于声明其后的变量名；在变量名后面有个 `=`，用于给定此变量的初值为 `120`。

第2行中的 `生产总值 = 200` 即是将 `生产总值` 这个变量的值从原来的 `120` 改为现在的 `200`。

另外 `=` 符号可以用 `为` 代替，如第3行所示；上述语句与 `令日期 = 「2021-09-25」` 是等价的。
 
> 注意：Zn 使用中文编程，关键字及变量名称都是中文，且关键字和变量名之间无需空格；所以在变量命名时，需要适当避开 `令`，`如果`，`每当` 等关键字；如果实在需要命名诸如 `将军令` 的变量，需要在变量名前后各加一个 `·`，变成 `·将军令·` 

## 变量的基本类型

Zn 的变量支持以下几种基本类型：

```zn
令数值* = 123.45
令文本* =「千里之行，始于足下。」 注：「也可以使用此引号 “ ” 来包围文本」
令布尔值* = 真                 注：「布尔类型有两个值，一为 「真」，一为「假」」

令数组* =【1，3，5，7，9】
令列表* =【
    名称 ==「清华大学」         注：中间的符号是两个「等于」号
    分数线 == 762
    录取要求 ==【「文科」，「理科」】
】

令空值* = 空
```

上面的代码分别列举出了 Zn 所支持的几种基本类型：`数值`、`文本`、`布尔`、`数组`、`列表` 以及特殊的 `空` 类型. 每种类型都有其特有的处理方法，具体的用法将会在后文说明。

> 注：`+`、`-`、`*`、`/` 可以参与到变量名的命名当中，如 `数值*` 是一个符合要求的变量名.

## 数值的四则运算

和大多数编程语言不同，`+`、`-`、`*`、`/` 并不是关键字，亦不参与到四则运算当中。数据的四则运算采用「声明」的形式，如下所示：

```zn
令加法结果 =【120，80】之和         注：「120 + 80 = 200」
令减法结果 =【15，8，6】之差        注：「15 - 8 - 6 = 1」
令乘法结果 =【27.47，2.09】之积     注：「27.47 * 2.09 = 57.4123」
令除法结果 =【48，3】之商           注：「48 / 3 = 16」
```

其中，用`【 】` 括住的是一个数组，里面的每一个元素都是数值。数组需要至少一个元素，其中第一个元素为被加数/被减数，后面的元素依次对前面的结果进行相加/相减/相乘/相除。`之` 是一个关键字（可以用 `的` 替代），后面的 `和/差/积/商` 分别代表着这个数组的属性。显然， `xx 之和` 得到的结果即是数组内各个元素之和，对于其他属性以此类推。

同时对于数值，也可以用属性及方法对于数值 `+1` 或 `-1`：

```zn
令数值 = 8.5
令X1 = 数值之+1         注：X1 = 8.5 + 1 = 9.5，等同于 令X1 =【数值，1】之和
令X2 = 数值之-1         注：X2 = 8.5 - 1 = 7.5，等同于 令X1 =【数值，1】之差

令Y = 数值之（+1）       注：数值的值从 8.5 加1到 9.5；并将值传递给 Y
令Z = Y之（-1）         注：数值的值从 9.5 减1到 8.5；并将值传递给 Z
```

> 注：数值的属性仅限于 `+1`, `-1`；并没有 `+2`、`-2`、`*2` 这样的属性！

## 流程控制

Zn 支持 `如果`、`每当`、`遍历` 三种流程控制语句。

### `如果`语句

`如果` 语句用于控制具体执行哪一段代码，如果设定条件的值为 `真`，那么执行 `如果` 语句下的语句块；否则不执行。具体逻辑如下所示：

```zn
如果【15，6】之和 = 20：
    令文本 = 「15 + 6 等于 20」
    （显示：文本）
否则：
    （显示：「15 + 6 不等于 20」）
```

在上面的程序中，第1行指定了一个 `【15，6】之和 = 20` 的表达式；这个表达式的结果只会有「真」和「假」两种可能。如果表达式的结果为「真」，那么即执行「如果」语句下，第2行和第3行的程序；如果是「假」，那么执行「否则」语句下，第5行的程序。

显然，15 + 6 = 21 不等于 20，因此程序最终会在终端显示「15 + 6 不等于 20」。

> 注1：Zn 以缩进区分语句块。一个缩进单元为 4个空格 或者 1个Tab。同一个文件里的所有缩进类型要么都是空格，要么都是Tab，不能空格和Tab混用。后面的所有语句亦然。  
> 注2：「否则」语句可以省略，但是「如果」语句下必须要另起一行，并添加一个缩进单元编写里面的逻辑。  
> 注3：（显示：xx） 是 Zn 里面的一个内置方法，调用此方法可以在终端里面打印出变量的值。   

### `每当`语句

`每当` 语句可以重复执行对应的语句块，直到指定条件不再满足为止，如下所示：

```zn
令倒计时 = 10
每当倒计时 > 0：
    （显示：「倒计时：」、倒计时）
    倒计时之（-1）
（显示：「倒计时结束」）
```

在上面的程序中，只要第2行中的 `倒计时 > 0` 条件成立，「每当」语句后面的语句块（第3行，第4行）都会从头到尾执行一遍；并且每执行一次，变量 `倒计时` 的值都会减1；当执行第10遍结束时，`倒计时`的值为0，因此不符合条件，跳出循环，执行第5行。

### `遍历`语句

类似功能的还有 `遍历` 语句：此语句将指定数组或者列表从头到尾遍历一遍其中的每一个元素，并执行对应的语句块，如下所示：

```zn
令数列 =【4，8，16，32，64，128，256】
令结果 = 0
以元素遍历数列：
    结果 =【元素，结果】之和

（显示：结果）          注：最后的结果是508
```

在上面的程序中，`遍历` 语句声明了一个 `元素` 的变量，此变量的值随着每一次循环依次为 4，8，16，... 直到 256 为止。在每次循环时，`结果` 的值都会和当前元素的值进行累加，在遍历结束后跳出循环，显示最终结果。（实际上上面的程序即是对于数列的元素求和。）

## 调用方法

所谓「方法」，即是定义一段可以重复使用的代码段，用于完成特定的任务。

举个例子，假设有两个数 A 和 B，我们可以用以下代码求出此两数之和：

```zn
如何求和？
    已知A、B
    返回【A，B】之和
```

上面的代码即是定义了一个「方法」：以「如何」关键字开头，方法名为「求和」；在 `？` 符号后面另起一行空四格，再去定义求和所需要的两个参数 A 和 B ：`已知A、B`。在第3行用「返回」关键字将 `【A，B】之和` 的结果返回出来，并结束整个流程。

如果我们已经知道 `A=4, B=5` 的话，那么调用「求和」方法的代码如下所示：

```zn
令结果 =（求和：4、5）          注：结果为 9
```

在上面的代码中，`（求和：4、5）` 本身是一个表达式；其中 `（ ）` 表示调用某个方法，「求和」为方法名，而后面的参数 `4` 和 `5` 分别对应于「求和」方法中的 A 和 B。最后的结果即是 「返回」语句所返回的值 `【4，5】之和`，显然结果为 `9`。

我们还可以借助上面提供的流程控制语句，在方法里实现更加复杂的逻辑，如下面的「寻找最大值」所示：

```zn
如何寻找最大值？
    已知数组

    令最大值 = 空
    以X遍历数组：
        如果最大值 = 空，或X > 最大值：
            最大值 = X

    返回最大值

（寻找最大值：【9，-2，16，7，3，9】）          注：返回 16
```

上面的逻辑即是从一个数组里面找到数值最大的数，并把结果返回给用户。实现方法非常简单：定义一个变量用于存储最大值，而后从数组里面从头到尾遍历所有的元素：如果这个元素的值大于「最大值」，那就把这个元素设置为「最大值」。

## 定义对象

每一个变量的值都有其对应的类型。如前所述，目前已经支持了 `数值`、`文本`、`数组` 等基本类型；而在很多情况下，我们需要自定义类型用于更好地描述想要做的事情，如下例说明：

```zn
定义用户
    其姓名 =「」
    其出生年份 =「2021」
    其出生月份 =「9」
    其性别 =「男」
    其邮箱 =「mock@mock.com」

    是为姓名

    何为其出生年月？
        返回「{#1}-{#2}」之（格式拼接：其出生年份、其出生月份）

    如何制作名片？
        返回「
            姓名：{#1}
            出生年月：{#2}
            邮箱：{#3}
        」之（格式拼接：其姓名、其出生年月、其邮箱）
```

上面的例子即是定义了一个名为「用户」的类型：这个类型包含了「姓名」、「性别」、「邮箱」等属性。

接下来我们可以使用这个「用户」类型去派生出几个对象：

```zn
令小明成为用户：「小明」
令小张成为用户：「小张」
```

这里小明、小张两个变量都属于「用户」对象；注意在派出对象时需要使用 `成为` 关键字，而不是 `为` 关键字或者 `=`。在小明 `成为` 用户对象时，冒号后面的「小明」即对应了「姓名」的属性，因此『小明』这个对象的「姓名」属性对应的值为「小明」；同样的道理，『小张』这个对象的「姓名」属性对应的值应为「小张」。

创建对象之后，接下来可以修改及读取在「用户」类型里已列举的属性，如下所示：

```
小明的邮箱 = 「xiaoming@gmail.com」         注：「『的』也可以用『之』代替」
（显示：小明的邮箱）        注：「显示「xiaoming@gmail.com」而不是默认的「mock@mock.com」」
```

同时我们可以直接调用这个对象对应的方法：

```zn
对于小明（制作名片），得到名片      注：「或者写为『小明之（制作名片），得到名片』 两者从语法上是一致的」
```

关于对象和类型的更多细节请参考《用户手册》。

